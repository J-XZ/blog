---
layout: post 
category: CSAPP 
---
---

## Part A

编写Y86-64代码实现C语言代码给定的功能(在sim/misc文件夹完成)

链表结构体

```c
/* $begin examples */
/* linked list element */
typedef struct ELE {
    long val;
    struct ELE *next;
} *list_ptr;
```

### 1、求链表元素的和

```c
/* sum_list - Sum the elements of a linked list */
long sum_list(list_ptr ls) {
    long val = 0;
    while (ls) {
        val += ls->val;
        ls = ls->next;
    }
    return val;
}
```

```c
#	sum.ys

#   寄存器
#   rax rbx rcx rdx rsp rbp rsi rdi r8~r14
#   条件码
#   ZF  SF  OF
#   指令集
#   halt    nop     rrmovq  irmovq  mrmovvq
#   subq    addq    andq    xorq
#   jle     jl      je      jne     jge     jg
#   call    ret     qushq   popq

#y86总是从0开始执行
#手动设置程序段的起始位置是0
    .pos 0
    irmovq stack, %rsp # 设置栈顶指针
    rrmovq %rsp,%rbp #设置栈底指针
    call main
    halt

main:
    irmovq ele1, %rdi  
    call sum_list
    ret

#参数 指向链表头结点的指针　%rdi
#返回 链表元素之和　　　　　%rax
#将当前节点的值加到总和里面，然后令指针指向下一个节点
#然后判断指针是否为空（==0），如果非空则重复上一步
sum_list:
loop1:
    mrmovq (%rdi),%rbx
    addq %rbx,%rax
    mrmovq 8(%rdi),%rdi
    andq %rdi,%rdi
jne loop1
    ret

.align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

#必须手动设置栈的位置
.pos 0x1000
stack:

```

![2021-08-08 22-46-56 的屏幕截图](../../www/assets/pic/2021-08-08 22-46-56 的屏幕截图.png)

### 2、递归求链表元素的和

```c
/* rsum_list - Recursive version of sum_list */
long rsum_list(list_ptr ls) {
    if (!ls)
        return 0;
    else {
        long val = ls->val;
        long rest = rsum_list(ls->next);
        return val + rest;
    }
}
```

```c
#   寄存器
#   rax rbx rcx rdx rsp rbp rsi rdi r8~r14
#   条件码
#   ZF  SF  OF
#   指令集
#   halt    nop     rrmovq  irmovq  mrmovvq
#   subq    addq    andq    xorq
#   jle     jl      je      jne     jge     jg
#   call    ret     qushq   popq

#y86总是从0开始执行
#手动设置程序段的起始位置是0
    .pos 0
    irmovq  stack,  %rsp # 设置栈顶指针
    rrmovq  %rsp,   %rbp #设置栈低指针
    call    main
    halt

main:
    irmovq  ele1,   %rdi   
    call    rsum_list
    ret

#参数　指向链表头结点的指针　%rdi
#返回　链表元素之和　　　　　%rax
rsum_list:
    #清空存储链表元素和的寄存器
    irmovq  $0,     %rax
    #测试当前rdi指向的链表节点是不是null
    andq    %rdi,   %rdi
    #如果是null直接返回0（返回时rax寄存器存的值就是之前清空的0）
    je      return
    #如果当前指向的节点不是空节点
    #先将当前节点指向的值存到栈中
    mrmovq  (%rdi), %rbx
    pushq   %rbx
    #令指针指向下一个节点
    mrmovq  8(%rdi),%rdi
    #递归调用自身，求下一个节点开头的链表的元素和
    #函数返回后下一个节点开头的链表元素和就存储在rax中
    call    rsum_list
    #恢复局部变量rbx的值，也就是当前节点的值
    popq    %rbx
    #将当前节点的值和之后节点值的和相加，得到整个链表的元素和，
    #存在rax中
    addq    %rbx,   %rax
return:
    ret

.align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

#必须手动设置栈的位置
.pos 0x1000
stack:

```

![](../../www/assets/pic/2021-08-08 22-45-27 的屏幕截图.png)

### 3、拷贝数据并返回异或和

```c
#   sum.ys

#   寄存器
#   rax rbx rcx rdx rsp rbp rsi rdi r8~r14
#   条件码
#   ZF  SF  OF
#   指令集
#   halt    nop     rrmovq  irmovq  mrmovq  rmmovq
#   subq    addq    andq    xorq
#   jle     jl      je      jne     jge     jg
#   call    ret     qushq   popq

#y86总是从0开始执行
#手动设置程序段的起始位置是0
    .pos 0
    irmovq stack, %rsp  #设置栈顶指针
    rrmovq %rsp,%rbp    #设置栈底指针
    call main
    halt

main:
    irmovq ele1, %rdi #源地址
    irmovq dest, %rsi #目标地址
    irmovq $0x6,%rdx  #需要复制6*8=48个字节
    #因为c代码中是用long型变量作为中间值进行内存复制的
    call copy_block
    ret

#参数1    指向源内存地址的指针　      %rdi
#参数2    指向目的内存地址的指针      %rsi
#参数3    复制的8字节数             %rdx
#返回     异或和                   %rax
copy_block:
    # 清空异或和
    irmovq $0x0,%rax
loop:
    irmovq $0x0,%rbx
    subq %rbx,%rdx
    # 将rdx与0比较大小，如果rdx<=0就返回
    jle return
    # 将rdx指向的8个字节存到rbx
    mrmovq (%rdi),%rbx
    # 更新异或和
    xorq %rbx,%rax
    # 将这8个字节存到指定的目的地址
    rmmovq %rbx,(%rsi)
    # 源地址和目的地址指针各自后移一个long型变量
    irmovq $0x8,%rbx
    addq %rbx,%rdi
    addq %rbx,%rsi
    # 记录复制量的变量减1
    irmovq $0x1,%rbx
    subq %rbx,%rdx
    # 回到while循环头部
    jmp loop
return:
    ret


.align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

.align 8
dest:
    .quad 0xffffffffffffffff
    .quad 0xffffffffffffffff
    .quad 0xffffffffffffffff
    .quad 0xffffffffffffffff
    .quad 0xffffffffffffffff
    .quad 0xffffffffffffffff

#必须手动设置栈的位置
.pos 0xfff
stack:
```

![2021-08-08 23-46-44 的屏幕截图](../../www/assets/pic/2021-08-08 23-46-44 的屏幕截图.png)

源指指向的内容

![2021-08-08 23-47-21 的屏幕截图](../../www/assets/pic/2021-08-08 23-47-21 的屏幕截图.png)

用python验证异或和正确：

![2021-08-08 23-40-25 的屏幕截图](../../www/assets/pic/2021-08-08 23-40-25 的屏幕截图.png)

## Part B

扩展SEQ处理器，通过修改seq-full.hcl令其支持iaddq指令。
对应书本习题：
![IMG_6379](../../www/assets/pic/IMG_6379-16284767666991.jpeg)
![IMG_6380](../../www/assets/pic/IMG_6380-16284767798482.jpeg)

<img src="../../www/assets/pic/IMG_6377.jpeg" alt="IMG_6377" style="zoom:50%;" />

y86处理器的阶段

#### 取指

y86指令编码长度从一个字节到10个字节不等

首先从PC指向的地址位置读取指令的第一个字节，这个字节的高四位是代码部分**icode**，低4位是功能部分**ifunc**，功能部分只有在一组相关指令共用一个代码时才起作用

比如addq和subq都属于opq指令家族，他们拥有相同的icode但是不同的ifunc

现有指令的代码段和功能段：

<img src="../../www/assets/pic/IMG_6381.jpeg" alt="IMG_6381" style="zoom:50%;" />

再根据第一个字节的指示（根据不同的指令类型）最多读取两个寄存器字段和一个立即数字段

两个寄存器字段共用一个字节

立即数字段因为是64位机，所以使用8个字节

其他指令结构参考

<img src="../../www/assets/pic/IMG_6382.jpeg" alt="IMG_6382" style="zoom:50%;" />

取指阶段记录valp等于PC加上当前已取出指令的长度

#### 译码

从寄存器文件读入最多两个操作数（读指令中指明的寄存器）

#### 执行

调用ALU更新目标寄存器，如果是跳转指令，判定是否需要跳转

#### 访存

将数据写入内存或从内存读出数据

#### 写回

最多写两个结果到寄存器文件

#### 更新PC

### 一些现有指令的各阶段参考：

![IMG_6383](../../www/assets/pic/IMG_6383.jpeg)

### 对于iaddq指令

#### 取指

icode:ifun	$\gets$	M1[PC]		左侧写法中M1[PC]表示按照PC指示的地址，从内存读取一个字节的内容

rA:rB			$\gets$	M1[PC+1]

valC			 $\gets$	M8[PC+2]

valP			 $\gets$	PC+10

#### 译码

valB			$\gets$	R[rB]	从寄存器rB中取值

#### 执行

valE			$\gets$	valB aluadd valC

set	Condition Code

#### 访存

NONE

#### 写回

R[rB]			$\gets$	valE

#### 更新PC

PC			$\gets$	valP

**用git的形式展示对于硬件描述语言的修改**

![2021-08-09 15-01-53 的屏幕截图](../../www/assets/pic/2021-08-09 15-01-53 的屏幕截图.png)
![2021-08-09 15-02-06 的屏幕截图](../../www/assets/pic/2021-08-09 15-02-06 的屏幕截图.png)
![2021-08-09 15-02-15 的屏幕截图](../../www/assets/pic/2021-08-09 15-02-15 的屏幕截图.png)

上图第二列行号151行有笔误，应该是IADDQ不需要将访存结果写到寄存器

运行结果

![2021-08-09 14-22-32 的屏幕截图](../../www/assets/pic/2021-08-09 14-22-32 的屏幕截图.png)
![2021-08-09 14-24-13 的屏幕截图](../../www/assets/pic/2021-08-09 14-24-13 的屏幕截图.png)

## Part C

修改流水线化y86-64处理器的硬件和ncopy.ys软件，使其运行尽可能快

首先在修改流水线处理器硬件，使其支持指令iaddq

![2021-08-09 16-14-48 的屏幕截图](../../www/assets/pic/2021-08-09 16-14-48 的屏幕截图.png)
![2021-08-09 16-15-02 的屏幕截图](../../www/assets/pic/2021-08-09 16-15-02 的屏幕截图.png)

使用

```bash
/archlab-handout/sim/pipe$sudo make  VERSION=full
```

重新生成处理器模拟器和测试代码

然后使用

```bash
/archlab-handout/sim/pipe$ ./correctness.pl && ./benchmark.pl
```

检查正确性和打分

仅实现iaddq然后在ncopy.ys中使用iaddq指令替换原先用寄存器存储加数的addq指令，CPE从15.18 $\Rightarrow$13.70

再用iaddq加上一个负立即数代替原先使用寄存器存储减数的subq指令，CPE从13.70$\Rightarrow$12.70

从硬件描述语言来看，分支预测总是预测会进行跳转，那么修改进入循环的代码

![2021-08-09 16-53-24 的屏幕截图](../../www/assets/pic/2021-08-09 16-53-24 的屏幕截图.png)

第21行的jg Loop会直接跳到Loop循环处，对于所有len>0的情况，可以减少一次预测错误

经测试这样微小的改动也是有效的，修改后CPE从12.70$\Rightarrow$12.55

#### 要进一步优化代码结构，实现对于流水线处理器友好的代码，并尽可能减少计算次数

使用4*1循环展开

```c
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
   # Loop header
   rrmovq %rdx,%r8
   xorq %rax,%rax    # count = 0;
   iaddq $-4,%r8
   jge Loop        # if len>=4
   jmp Little    # if len<4

Loop:
    mrmovq (%rdi), %r8 # read val from src...
    mrmovq 8(%rdi),%r9
    mrmovq 16(%rdi),%r10
    mrmovq 24(%rdi),%r11

   rmmovq %r8, (%rsi) # ...and store it to dst
   rmmovq %r9,8(%rsi)
   rmmovq %r10,16(%rsi)
   rmmovq %r11,24(%rsi)

   andq %r8,%r8
   jle next1
   iaddq $1,%rax
next1:
   andq %r9,%r9
   jle next2
   iaddq $1,%rax
next2:
    andq %r10,%r10
   jle next3
   iaddq $1,%rax
next3:
    andq %r11,%r11
   jle next4
   iaddq $1,%rax
next4:

    iaddq $32,%rdi
    iaddq $32,%rsi
    iaddq $-4,%rdx
    rrmovq %rdx,%r8
    iaddq $-4,%r8
    jge Loop

Little:
    andq %rdx,%rdx
    je Done
Loop2:
    mrmovq (%rdi),%r8
    rmmovq %r8,(%rsi)
    andq %r8,%r8
    jle next5
    iaddq $1,%rax
next5:
    iaddq $8,%rdi
    iaddq $8,%rsi
    iaddq $-1,%rdx
    jg Loop2

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
   ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
```

![2021-08-09 17-54-19 的屏幕截图](../../www/assets/pic/2021-08-09 17-54-19 的屏幕截图.png)

---

![2021-08-09 18-00-53 的屏幕截图](../../www/assets/pic/2021-08-09 18-00-53 的屏幕截图.png)

修改为默认预测跳到Loop2，因为这个循环只有全部元素循环完毕时才会需要跳出，跳到Loop2的情况总是多一些

![2021-08-09 18-02-42 的屏幕截图](../../www/assets/pic/2021-08-09 18-02-42 的屏幕截图.png)

---

对于4*1循环展开剩下的0~3个元素的计算进行特殊处理，并使用类似内联函数思想（通过复制一些代码代替跳转），得到下面这种：

```c
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
   rrmovq %rdx,%r8
   xorq %rax,%rax    # count = 0;
   iaddq $-4,%r8
   jge Loop        # if len>=4
                    # jmp Less_or_equal_3
    iaddq $-1,%rdx
    je Last_one
    jg Three_or_2
    ret

Loop:
    mrmovq (%rdi), %r8 # read val from src...
    mrmovq 8(%rdi),%r9
    mrmovq 16(%rdi),%r10
    mrmovq 24(%rdi),%r11

   rmmovq %r8, (%rsi) # ...and store it to dst
   rmmovq %r9,8(%rsi)
   rmmovq %r10,16(%rsi)
   rmmovq %r11,24(%rsi)

   andq %r8,%r8
   jle next1
   iaddq $1,%rax
next1:
   andq %r9,%r9
   jle next2
   iaddq $1,%rax
next2:
    andq %r10,%r10
   jle next3
   iaddq $1,%rax
next3:
    andq %r11,%r11
   jle next4
   iaddq $1,%rax
next4:

    iaddq $32,%rdi
    iaddq $32,%rsi
    iaddq $-4,%rdx
    rrmovq %rdx,%r8
    iaddq $-4,%r8
    jge Loop

Less_or_equal_3:
    iaddq $-1,%rdx
    je Last_one
    jg Three_or_2
    ret
Three_or_2:
    iaddq $-1,%rdx
    je Last_two
    mrmovq (%rdi),%r8  # read val from src...
    mrmovq 8(%rdi),%r9
    mrmovq 16(%rdi),%r10
    rmmovq %r8,(%rsi)  # ...and store it to dst
    rmmovq %r9,8(%rsi) # ...and store it to dst
    rmmovq %r10,16(%rsi)   # ...and store it to dst
    andq %r8,%r8
    jle next5
    iaddq $1,%rax
next5:
    andq %r9,%r9
    jle next6
    iaddq $1,%rax
next6:
    andq %r10,%r10
    jle next7
    iaddq $1,%rax
next7:
    ret

Last_one:
    mrmovq (%rdi),%r8  # read val from src...
    rmmovq %r8,(%rsi)  # ...and store it to dst
    andq %r8,%r8
    jle next9
    iaddq $1,%rax
next9:
    ret

Last_two:
    mrmovq (%rdi),%r8  # read val from src...
    mrmovq 8(%rdi),%r9
    rmmovq %r8,(%rsi)  # ...and store it to dst
    rmmovq %r9,8(%rsi) # ...and store it to dst
    andq %r8,%r8
    jle next10
    iaddq $1,%rax
next10:
    andq %r9,%r9
    jle next11
    iaddq $1,%rax
next11:
    ret

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
   ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
```

![2021-08-09 19-33-22 的屏幕截图](../../www/assets/pic/2021-08-09 19-33-22 的屏幕截图.png)

通过特判剩余的元素个数代替普通循环，对于4*1循环展开后剩余的0~3个元素有显著提升

---

**使用6*1循环展开，然后进一步优化剩余0~5个元素的处理方式**

循环展开部分和上述过程大同小异，主要是剩余0~5个元素的处理方式

使用一个树形结构判断需要在循环外处理多少个元素

<img src="../../www/assets/pic/IMG_6384.jpeg" alt="IMG_6384" style="zoom:50%;" />

首先将rdx的值与3相比，如果等于3就跳转到处理剩余3个元素的代码，如果小于3就再与1相比···；如果大于3就与4相比

通过树形的比较结构实现尽可能少的比较次数就能跳转到所需要的处理代码

由于剩余的元素数量一定在0~5之间，所以不需要写循环，只要写一个线性的代码片段，如果从头开始执行（下面代码的Tree5处）就处理5个元素，如果从Tree4处开始执行就处理4个元素

将判定rax是否需要加1的处理代码交错在复制内存的代码中，是为了在读内存和写内存的指令中间充分利用流水线，充分利用读写内存的等待时间

```c
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
    rrmovq %rdx,%r8
    iaddq $-6,%r8
    jge Larger_or_equal_6
    jmp Less_6
Larger_or_equal_6:
Loop1:
    mrmovq (%rdi),%r8
    mrmovq 8(%rdi),%r9
    mrmovq 16(%rdi),%r10
    mrmovq 24(%rdi),%r11
    mrmovq 32(%rdi),%r12
    mrmovq 40(%rdi),%r13
    rmmovq %r8,(%rsi)
    rmmovq %r9,8(%rsi)
    rmmovq %r10,16(%rsi)
    rmmovq %r11,24(%rsi)
    rmmovq %r12,32(%rsi)
    rmmovq %r13,40(%rsi)
    andq %r8,%r8
    jle not_add_1_1
    iaddq $1,%rax
not_add_1_1:
    andq %r9,%r9
    jle not_add_1_2
    iaddq $1,%rax
not_add_1_2:
    andq %r10,%r10
    jle not_add_1_3
    iaddq $1,%rax
not_add_1_3:
    andq %r11,%r11
    jle not_add_1_4
    iaddq $1,%rax
not_add_1_4:
    andq %r12,%r12
    jle not_add_1_5
    iaddq $1,%rax
not_add_1_5:
    andq %r13,%r13
    jle not_add_1_6
    iaddq $1,%rax
not_add_1_6:

    iaddq $48,%rsi
    iaddq $48,%rdi
    iaddq $-6,%rdx

    rrmovq %rdx,%r8
    iaddq $-6,%r8
    andq %r8,%r8
    jge Loop1

    xorq %r8,%r8
    xorq %r9,%r9
    xorq %r10,%r10
    xorq %r11,%r11
#0 <= old_rdx <= 5
Less_6:            
#rdx = old_rdx - 3
    iaddq $-3,%rdx      
#0 <= old_rdx <= 2
    jl zero_to_two      
#4 <= old_rdx <= 5
    jg four_to_five
#old_rdx = 3
    jmp Tree3           

four_to_five:
#rdx = old_rdx - 4
    iaddq $-1,%rdx   
#4 <= old_rdx <= 4
    je Tree4     

    mrmovq 32(%rdi),%r8 
    jmp Tree5

zero_to_two:  
#rdx = old_rdx - 1
    iaddq $2,%rdx  
#old_rdx = 1
    je Tree1  
#2 <= old_rdx <= 2
    jg Tree2            
    ret

Tree5:
#读r8的代码在jmp Tree5前面，也是为了在读内存和写内存的指令中间充分利用流水线
    rmmovq %r8,32(%rsi)
Tree4:
    mrmovq 24(%rdi),%r9
    andq %r8,%r8
    jle not_add_1_7
    iaddq $1,%rax
not_add_1_7:
    rmmovq %r9,24(%rsi)
Tree3:
    mrmovq 16(%rdi),%r10
    andq %r9,%r9
    jle not_add_1_8
    iaddq $1,%rax
not_add_1_8:
    rmmovq %r10,16(%rsi)
Tree2:
    mrmovq 8(%rdi),%r11
    andq %r10,%r10
    jle not_add_1_9
    iaddq $1,%rax
not_add_1_9:
    rmmovq %r11,8(%rsi)
Tree1:
    mrmovq (%rdi),%r12
    andq %r11,%r11
    jle not_add_1_10
    iaddq $1,%rax
not_add_1_10:
    rmmovq %r12,(%rsi)
    andq %r12,%r12
    jle not_add_1_11
    iaddq $1,%rax
not_add_1_11:

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
   ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
```

![2021-08-09 19-33-22 的屏幕截图](../../www/assets/pic/2021-08-09 19-33-22 的屏幕截图-16285210762043.png)

---

按照上述思路写成7*1循环展开

<img src="../../www/assets/pic/IMG_6385.jpeg" alt="IMG_6385" style="zoom:50%;" />

```c
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
    rrmovq %rdx,%r8
    iaddq $-7,%r8
    jge Larger_or_equal_7
    jmp Less_7
Larger_or_equal_7:
Loop1:
    mrmovq (%rdi),%r8
    mrmovq 8(%rdi),%r9
    mrmovq 16(%rdi),%r10
    mrmovq 24(%rdi),%r11
    mrmovq 32(%rdi),%r12
    mrmovq 40(%rdi),%r13
    mrmovq 48(%rdi),%r14
    rmmovq %r8,(%rsi)
    rmmovq %r9,8(%rsi)
    rmmovq %r10,16(%rsi)
    rmmovq %r11,24(%rsi)
    rmmovq %r12,32(%rsi)
    rmmovq %r13,40(%rsi)
    rmmovq %r14,48(%rsi)
    andq %r8,%r8
    jle not_add_1_1
    iaddq $1,%rax
not_add_1_1:
    andq %r9,%r9
    jle not_add_1_2
    iaddq $1,%rax
not_add_1_2:
    andq %r10,%r10
    jle not_add_1_3
    iaddq $1,%rax
not_add_1_3:
    andq %r11,%r11
    jle not_add_1_4
    iaddq $1,%rax
not_add_1_4:
    andq %r12,%r12
    jle not_add_1_5
    iaddq $1,%rax
not_add_1_5:
    andq %r13,%r13
    jle not_add_1_6
    iaddq $1,%rax
not_add_1_6:
    andq %r14,%r14
    jle not_add_1_61
    iaddq $1,%rax
not_add_1_61:
    iaddq $56,%rsi
    iaddq $56,%rdi
    iaddq $-7,%rdx

    rrmovq %rdx,%r8
    iaddq $-7,%r8
    andq %r8,%r8
    jge Loop1

    xorq %r8,%r8
    xorq %r9,%r9
    xorq %r10,%r10
    xorq %r11,%r11
    xorq %r12,%r12

Less_7:             #0 <= old_rdx <= 6
    iaddq $-3,%rdx      #rdx = old_rdx - 3
    jl zero_to_two      #0 <= old_rdx <= 2
    jg four_to_five     #4 <= old_rdx <= 6
    jmp Tree3           #old_rdx = 3

four_to_five:
    iaddq $-2,%rdx      #rdx = old_rdx - 5
    je Tree5            #5 <= old_rdx <= 5
    jl Tree4            #4 <= old_rax <= 4
    mrmovq 40(%rdi),%r8
    jmp Tree6

zero_to_two:
    iaddq $2,%rdx       #rdx = old_rdx - 1
    je Tree1            #old_rdx = 1
    jg Tree2            #2 <= old_rdx <= 2
    ret
Tree6:
    rmmovq %r8,40(%rsi)
Tree5:
    mrmovq 32(%rdi),%r9
    andq %r8,%r8
    jle not_add_1_7x
    iaddq $1,%rax
not_add_1_7x:
    rmmovq %r9,32(%rsi)
Tree4:
    mrmovq 24(%rdi),%r10
    andq %r9,%r9
    jle not_add_1_7
    iaddq $1,%rax
not_add_1_7:
    rmmovq %r10,24(%rsi)
Tree3:
    mrmovq 16(%rdi),%r11
    andq %r10,%r10
    jle not_add_1_8
    iaddq $1,%rax
not_add_1_8:
    rmmovq %r11,16(%rsi)
Tree2:
    mrmovq 8(%rdi),%r12
    andq %r11,%r11
    jle not_add_1_9
    iaddq $1,%rax
not_add_1_9:
    rmmovq %r12,8(%rsi)
Tree1:
    mrmovq (%rdi),%r13
    andq %r12,%r12
    jle not_add_1_10
    iaddq $1,%rax
not_add_1_10:
    rmmovq %r13,(%rsi)
    andq %r13,%r13
    jle not_add_1_11
    iaddq $1,%rax
not_add_1_11:

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
   ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
```

![2021-08-09 23-22-26 的屏幕截图](../../www/assets/pic/2021-08-09 23-22-26 的屏幕截图.png)

---

用同样的思路并进一步优化诸多细节，可以得到最终答案

```c
#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
###从这里开始的代码是处理10*1循环展开###
                            #y86处理器没有复杂的流水线控制机制（只有一条流水线、只有一个ALU）
                            #所以我们的循环展开主要节省的是循环边界的指令计算次数
                            #比如如果步长为1的时候循环边界需要计算n次
                            #那么使用10*1循环展开，循环边界就只要计算n/10次
                            #而对于循环内部 需要执行的操作，是否循环展开都将使用相同的时间
loop:                       #因为限制代码量不能超过1000字节，所以没有初始化rax寄存器
                            #y86-64默认保证未初始化的寄存器初始值为0，所以就省略了初始化寄存器
                            #如果是实际编写一个供任意调用的函数，不初始化寄存器是不行的
    iaddq $-10,%rdx         #将rdx减10，然后判断rdx是否大于10
    jl l_0_9                #如果不足十个元素，就跳转到处理剩余元素的代码
                            #如果至少有十个元素，就运行下面的代码，将复制并计数10个元素
                            #条件跳转指令默认预测会跳转，这里默认会预测跳到不足10个元素的情况
                            #对于小样例有优化
move_10:
    mrmovq (%rdi),%r8       #i0 一次性读两个元素，是为了防止两条相邻的指令读取同样的内存内容造成流水线插入气泡等待
    mrmovq 8(%rdi),%r9      #i1 ix，i表示input，从内存读入第x个元素
    rmmovq %r8,(%rsi)       #o0 ox，o表示写到内存，将第x个元素写到内存
    rmmovq %r9,8(%rsi)      #o1
    andq %r8,%r8            #t0
    jle na0                 #判定元素是否大于0，如果大于0就给rax加1
    iaddq $1,%rax           #计数部分的分支跳转是无法优化和避免的，因为每个数字都有50%的概率是正数
na0:
    andq %r9,%r9            #t1
    jle na1
    iaddq $1,%rax
na1:
    mrmovq 16(%rdi),%r8     #i2
    mrmovq 24(%rdi),%r9     #i3
    rmmovq %r8,16(%rsi)     #o2
    rmmovq %r9,24(%rsi)     #o3
    andq %r8,%r8            #t2
    jle na2
    iaddq $1,%rax
na2:
    andq %r9,%r9            #t3
    jle na3
    iaddq $1,%rax
na3:
    mrmovq 32(%rdi),%r8     #i4
    mrmovq 40(%rdi),%r9     #i5
    rmmovq %r8,32(%rsi)     #o4
    rmmovq %r9,40(%rsi)     #o5
    andq %r8,%r8            #t4
    jle na4
    iaddq $1,%rax
na4:
    andq %r9,%r9            #t5
    jle na5
    iaddq $1,%rax
na5:
    mrmovq 48(%rdi),%r8     #i6
    mrmovq 56(%rdi),%r9     #i7
    rmmovq %r8,48(%rsi)     #o6
    rmmovq %r9,56(%rsi)     #o7
    andq %r8,%r8            #t6
    jle na6
    iaddq $1,%rax
na6:
    andq %r9,%r9            #t7
    jle na7
    iaddq $1,%rax
na7:
    mrmovq 64(%rdi),%r8     #i8
    mrmovq 72(%rdi),%r9     #i9
    rmmovq %r8,64(%rsi)     #o8
    rmmovq %r9,72(%rsi)     #o9
    andq %r8,%r8            #t8
    jle na8
    iaddq $1,%rax
na8:
    andq %r9,%r9            #t9
    jle na9
    iaddq $1,%rax
na9:                        #到这里复制并计数10个元素已毕
                            #下面的代码是更新循环变量
    iaddq $80,%rdi          #将指向源地址的指针后移10个8位long型变量
    iaddq $80,%rsi          #同上将指向目的地址的指针后移80字节
    iaddq $-10,%rdx         #将计数变量减去10
                            #其实可以不在这里减，而是直接跳转到循环头部，
                            #用头部的判断确定是否还要再来一轮循环
                            #但是在这里减可以充分利用分支跳转的默认预测方式（预测会跳转）
    jge move_10             #分支跳转指令默认会跳转，默认预测再来一轮循环，对于多轮循环的情况有优化
###从这里开始的代码是处理不足10个元素无法使用循环展开的情况###
                            #下面的代码是处理不足10个剩余元素的
                            #可以用循环处理剩余的元素，但是循环会造成大量的冗余计算
                            #所有处理循环边界（rdx--、判断是否跳出循环）的计算都是冗余的
                            #这些计算对于实际复制元素并计数是没有用的
                            #可以使用平衡二叉树搜索剩余元素的个数，尽可能减少大小比较次数
                            #使用循环操作，你需要O(n)次冗余计算，n是剩余元素个数
                            #但是使用二叉树搜索剩余元素个数，只需要O(log n)次冗余计算
                            #高效地跳转到对应处理代码
l_0_9:                      #记住，从上面运行到这行代码时，rdx是处于-10的状态
                            #运行到这里时，剩余元素数量可能是0~9
    iaddq $7,%rdx           #rdx+=7，相当与计算实际剩余元素数量-3
    jl l_0_2                #如果实际剩余元素数量<3，跳转到处理剩余0~2个元素的代码
    jg l_4_9                #如果实际剩余元素数量>3，跳转第处理剩余4~0个元素的代码
    je l_3                  #如果实际剩余元素数量=3，跳转第处理剩余3个元素的代码
l_4_9:                      #运行到这里时，剩余元素数量可能是4~9
                            #因为前面的操作rdx的值等同于实际剩余元素数量-3
    iaddq $-3,%rdx          #rdx-=3，相当于实际剩余元素数量-6
    jg l_7_9                #如果实际剩余元素数量>6，跳转到处理剩余7~9个元素的代码
    je l_6                  #如果实际剩余元素数量=6，跳转到处理剩余6个元素的代码
    iaddq $1,%rdx           #rdx+=1，相当于实际剩余元素数量-5
    je l_5                  #如果实际剩余元素数量=5，跳转到处理剩余5个元素的代码
    jl l_4                  #如果实际剩余元素数量<5，跳转到处理剩余4个元素的代码
l_0_2:                      #运行到这里时，剩余元素数量可能是0~2
    iaddq $2,%rdx           #rdx+=2，相当于实际剩余元素数量-1
    je l_1                  #如果实际剩余元素数量=1，跳转到处理剩余1个元素的代码
    iaddq $-1,%rdx          #rdx-=1，相当于实际剩余元素数量-2
    je l_2                  #如果实际剩余元素数量=2，跳转到处理剩余两个元素的代码
    ret                     #如果实际剩余元素数量既不是1也不是2，那就是0，直接返回
l_7_9:                      #运行到这里时，剩余元素数量可能是7~9
    iaddq $-2,%rdx          #rdx-=2，相当于实际剩余元素数量-8
    jl l_7                  #如果实际剩余元素数量<8，跳转到处理剩余7个元素的代码
    je l_8                  #如果实际剩余元素数量=8，跳转到处理剩余8个元素的代码
###下面是对于已知具体剩余元素个数时的处理###
                            #基本思路是：
                            #假设我们剩余2个元素，定义我们所需的复制和计数的全部操作为操作x_2
                            #对于剩余4个元素的情形，定义所需的复制和计数的全部操作为操作x_4
                            #x_2一定是x_4的子集
                            #把代码按照 处理较多剩余元素->处理较少剩余元素的顺序组织
                            #下面的l_9指示的代码是处理 复制并计数最远的第九个元素
                            #继续往下执行，就可以复制并计数最远的第八个元素、第七个元素，以此类推
                            #只要知道具体的剩余元素个数，找到处理代码的正确入口，就可以完成所有剩余元素的处理
l_9:                        #处理剩余9个元素代码的入口
    mrmovq 64(%rdi),%rbx    #这里用两条相邻的代码读写了相同的内存位置，流水线会插入气泡，但是无法优化
                            #因为它排在第一个，没有其他工作代码可以插入在这个空隙
                            #但是后面不会出现这样的问题
    rmmovq %rbx,64(%rsi)
    andq %rbx,%rbx          #处理剩余9个元素的代码只设置了标志位，但没有进行计数
                            #把计数操作移到了 读取和写入最远的第八个元素 的代码之间
                            #可以充分利用流水线的等待时间（防止一个周期的气泡）
l_8:                        #处理剩余8个元素代码的入口
    mrmovq 56(%rdi),%rcx
    jle na_9                #这里是实际处理最远的第九个元素的计数代码
                            #问题在于，如果只剩8个元素，那么直接跳转到l_8入口会进行一次不必要的标志位判断
                            #这个冗余的标志位判断会导致计数出错吗？
                            #不会
                            #以为上面二叉树搜索代码保证了每一个跳转到具体个数处理代码的跳转操作都是
                            #je或jl，都不会导致进行非必要的iaddq $1,%rax
    iaddq $1,%rax           #结果的正确性是可以保证的，但是对于这个非不要的条件跳转操作，
                            #我没有找到合适的方法规避
na_9:                       #除了刚好剩余9个或0个元素的情形不会进行额外的条件跳转
                            #其他情形都不得不额外进行一次条件跳转
                            #付出一次额外条件跳转代价的好处是，后面每一次读内存和写内存操作之间都不需要插入气泡
                            #这样的收益是值得付出一点点代价的
    rmmovq %rcx,56(%rsi)
    andq %rcx,%rcx
l_7:
    mrmovq 48(%rdi),%rbx
    jle na_8
    iaddq $1,%rax
na_8:                       #na_8:not_add_element_8
                            #表示第八个元素不大于0，不需要执行rax+1计数操作
    rmmovq %rbx,48(%rsi)
    andq %rbx,%rbx
l_6:
    mrmovq 40(%rdi),%rcx
    jle na_7
    iaddq $1,%rax
na_7:
    rmmovq %rcx,40(%rsi)
    andq %rcx,%rcx
l_5:
    mrmovq 32(%rdi),%rbx
    jle na_6
    iaddq $1,%rax
na_6:
    rmmovq %rbx,32(%rsi)
    andq %rbx,%rbx
l_4:
    mrmovq 24(%rdi),%rcx
    jle na_5
    iaddq $1,%rax
na_5:
    rmmovq %rcx,24(%rsi)
    andq %rcx,%rcx
l_3:
    mrmovq 16(%rdi),%rbx
    jle na_4
    iaddq $1,%rax
na_4:
    rmmovq %rbx,16(%rsi)
    andq %rbx,%rbx
l_2:
    mrmovq 8(%rdi),%rcx
    jle na_3
    iaddq $1,%rax
na_3:
    rmmovq %rcx,8(%rsi)
    andq %rcx,%rcx
l_1:
    mrmovq (%rdi),%rbx
    jle na_2
    iaddq $1,%rax
na_2:
    rmmovq %rbx,(%rsi)
    andq %rbx,%rbx
    jle Done
    iaddq $1,%rax

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
   ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
```

![2021-08-10 15-27-51 的屏幕截图](../../www/assets/pic/2021-08-10 15-27-51 的屏幕截图.png)

上述代码对于判定剩余元素个数所使用的搜索树结构如下：

<img src="../../www/assets/pic/IMG_6386.jpeg" alt="IMG_6386" style="zoom:50%;" />

在尽可能使之平衡的同时使得搜索到0、1、2这样较小的剩余元素个数的搜索路径比较短，可以优化小样例（我们对数据量很大的样例优化地越好，有时候就会造成数据量很小的样例冗余代码过多的副作用，优化数据量大的样例的同时竟可能避免这样的副作用是有必要的）。

---

补充：上述副作用就体现在，对于复制的元素个数很少时，代码的CPE时很大的，如果不考虑对于小样例的优化，复制0、1个元素的CPE可能会高达30几。而将复制大量元素的CPE降到比较低的程度是容易做到的

![2021-08-10 15-35-40 的屏幕截图](../../www/assets/pic/2021-08-10 15-35-40 的屏幕截图.png)
