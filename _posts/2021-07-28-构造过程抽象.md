---
layout: post 
category: SICP 
---

---

将简单的认识组合起来形成更复杂的方法：
$$
\left\{
	\begin{array}{}
		基本表达形式：语言所关心的最简单的个体\\
		组合的方法：通过他们可以从较简单的东西出发构造复杂的元素\\
		抽象的方法：通过它们为复合对象命名，并将它们当做单元去操作
    \end{array}
\right.
$$


lisp使用前缀表达式，适用于带有任意个实参的过程

```scheme
(+ 21 35 12 7)
```

合理的缩进有利于阅读表达式的含义

```scheme
#lang racket
(+ (* 3
      (+ (* 2 4)
         (+ 3 5)))
   (+ (- 10 7)
      6))
```

使用`define`将名字与计算对象关联

```scheme
(define size 2)
```

组合式的求值

- 求值该组合式的各个子表达式
- 将作为最左子表达式（运算符）的值的那个过程用于相应的实际参数，所谓实际参数就是其他子表达式（运算对象的值）

求子表达式的过程是递归的

用树表示求值规则

每个组合式用一个带分支的节点表示，分支对应组合式里的运算符和各个运算对象。终端节点（即那些不再发出分支的节点）表示运算符或者数值。

求值过程就像数值沿着树向上穿行。--> **树形积累**

<img src="../../www/assets/pic/tmp-1.png" style="zoom: 50%;" />

上图对应的计算式为：

```scheme
#lang racket
(* (+ 2
      (* 4
         6))
   (+ 3
      5
      7))
```

复合过程：为复合操作提供名字，以后将这样的操作作为一个单元使用

规则：

```scheme
(define (<name> <formal parameters>) <body>)
```

例如定义平方

```scheme
(define (square x)(* x x))
```

上述的x是形式参数

过程应用使用代换模型。

完全展开而后归约：正则序

先求值参数然后应用：应用序(实际解释器使用)。能避免表达式的重复求值。对于超出可以采用替换方式模拟的过程范围后，正则序会变得复杂。

## 条件表达式和谓词

cond

```scheme
(define (abs x)
  (cond ((> x 0)x)
        ((= x 0)x)
        ((< x 0)(- x))))
(define (abs_if x)
  (if (< x 0)
      (- x)
      x))
```

复合逻辑运算符：and、or、not

```scheme
(define (>= x y)
  (or (> x y)(= x y)))
```

牛顿法求平方根（递归）

```scheme
#lang scheme
(define (square x)
  (* x x))
(define (abs x)
  (if (< x 0)
      (- x)
      x))
(define (good-enough? guess x)
  (< (abs (-(square guess) x)) 0.0000001))
(define (improve guess x)
  (/ (+ guess (/ x guess)) 2))
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))
(define (sqrt x)
  (sqrt-iter 1.0 x))
(sqrt 3)
```

输出

```scheme
1.7320508100147274
```

过程可视作黑箱，过程内部的变量仅仅在过程内部有效

子过程可以局部化，定义到一个大过程内部，这样子过程内部使用的名字就不会和大过程以外使用的名字冲突

```scheme
(define (sqrt x)
  (define (square x)(* x x))
  (define (good-enough? guess x)
    (< (abs (- x (square guess))) 0.0000001))
  (define (average arg1 arg2)
    (/ (+ arg1 arg2) 2))
  (define (improve guess x)
    (average guess (/ x guess)))
  (define (sqrt-iter guess x)
    (if (good-enough? guess x)
        guess
        (sqrt-iter (improve guess x) x)))
  (sqrt-iter 1 x)
  )
(sqrt 3.0)
```

这种嵌套的定义称为块结构，因为子过程全部定义在块内部，他们其实可以公用x这个变量，而不需要显式传递x

```scheme
(define (sqrt x)
  (define (abs arg)(if (< arg 0)(- arg)arg))
  (define (square arg)(* arg arg))
  (define (good-enough? guess)
    (< (abs(- x (square guess)))0.00000001))
  (define (average arg1 arg2)(/ (+ arg1 arg2)2))
  (define (improve guess)
    (average guess (/ x guess)))
  (define (sqrt-iter guess)
    (if (good-enough? guess)guess(sqrt-iter (improve guess))))
  (sqrt-iter 1.0))
```

## 常用的模式

### 线性递归和迭代

计算阶乘（以下图片都是计算`(factorial 6)`在调试过程中对于栈情况的截图）

```scheme
;递归
(define (factorial n)
  (if (= n 0)
      1
      (* n (factorial (- n 1)))))
```

<img src="../../www/assets/pic/2021-07-28%2012-01-17%20%E7%9A%84%E5%B1%8F%E5%B9%95%E6%88%AA%E5%9B%BE.png" style="zoom:50%;" />

在表达式展开阶段里，计算过程构造起一个推迟执行的操作所形成的链条，栈要保存递归路径，因为下一级的值计算出来后要代入到上一级式子中进行计算


```scheme
;迭代
(define (factorial n)
  (define (fact-iter product counter)
  (if (> counter n)
      product
      (fact-iter (* counter product)
                 (+ 1 counter))))
  (fact-iter 1 1))
```

<img src="../../www/assets/pic/2021-07-28%2011-58-25%20%E7%9A%84%E5%B1%8F%E5%B9%95%E6%88%AA%E5%9B%BE.png" style="zoom:50%;" />

递归调用总是发生在尾部，不需要返回到上一级进行计算，栈中不需要保存沿途的调用。看起来是递归，其实是迭代！（类似kotlin的尾递归优化）

### 树形递归

```scheme
;树形递归求斐波那契数列中第n个数的值
(define (fib n)
  (cond ((= n 0)0)
        ((= n 1)1)
        (else (+ (fib (- n 1))
                 (fib (- n 2))))))
```

```scheme
;线性迭代求斐波那契数列中第n个数的值
#lang scheme
(define (fib n)
  (define (fib-iter a b n)
    (if (= n 0)
        b
        (fib-iter (+ a b) a (- n 1))))
  (fib-iter 1 0 n))

```

换硬币问题

```scheme
#lang scheme
(define (count-change amount)
  (cc amount 5))
(define (cc amount kinds-of-coins)
  (cond ((= amount 0)1)
        ((or (< amount 0)(= kinds-of-coins 0))0)
        (else (+(cc amount
                    (- kinds-of-coins 1))
                (cc (- amount
                       (first-denomination kinds-of-coins))
                    kinds-of-coins)))))
(define (first-denomination kinds-of-conis)
  (cond ((= kinds-of-conis 1) 1)
        ((= kinds-of-conis 2) 5)
        ((= kinds-of-conis 3) 10)
        ((= kinds-of-conis 4) 25)
        ((= kinds-of-conis 5) 50)))
```

高阶过程：操作过程的过程
